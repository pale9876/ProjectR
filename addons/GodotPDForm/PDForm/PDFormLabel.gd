# PDFormLabel.gd
@tool
class_name PDFormLabel extends Label


## Creates text in the document. Read on to learn the limitations.
##
## All labels are single line and continue writing forever on that one line. They do not regard
## the formatting of your text at all. Furthermore, all text is black and there is no way to
## change this (at least at this time).[br]
## To change the font and its size, you will need to provide the name of the font face as
## provided in [member PDFormBase.fonts_to_embed], and to preview the font you will need
## to actually set the font of this label (as through Theme Overrides/Font in the
## inspector).[br]
## [i]Author's note: I may come back and automate this a bit better, but for
## now this works.[/i]


## Sets the font face used in the exported document, as provided to the [PDFormBase] via
## [member PDFormBase.fonts_to_embed]. Helvetica is the default font and is the only
## included font in [b]PDForm[/b] (as it is included in [b]GodotPDF[/b]).[br]
## To preview this font, you will need to set the font via Theme Overrides/Font in the
## inspector.

@export var font_face : String = "Helvetica"

## Sets the font size (measured in .pt).
@export_range(2,128,1,"or_greater") var font_size : int = 12 :
	set(n):
		font_size = n
		add_theme_font_size_override("font_size", n)



const default_font : String = "res://addons/GodotPDForm/PDForm/HelveticaLight.tres"


func _ready() -> void:
	add_theme_color_override("font_color", Color(0,0,0,1))
	
	if get_theme_font("font").get_class() == "FontFile":
		var font : Font = load(default_font)
		add_theme_font_override("font", font)



func _get_configuration_warnings() -> PackedStringArray:
	var warnings: PackedStringArray = PackedStringArray()
	
	if !get_parent() is PDFormPage:
		warnings.append("Must be the child of a PDFormPage node.")
	
	if get_child_count() != 0:
		warnings.append("No children of this node will be added to this document.")
	
	return warnings
